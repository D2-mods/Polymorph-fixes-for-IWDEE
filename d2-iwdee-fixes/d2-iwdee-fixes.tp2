BACKUP ~d2-iwdee-fixes/backup~
AUTHOR ~Dan_P~
VERSION ~1.4~

LANGUAGE ~English~ ~english~ ~d2-iwdee-fixes/tra/english/setup.tra~


//Polymorph fixes for IWD:EE

BEGIN @11    // patch files
REQUIRE_PREDICATE GAME_IS ~iwdee~ @1
SUBCOMPONENT @10
LABEL ~D2-IWDEE-POLYMORPH~
INCLUDE ~%MOD_FOLDER%/install/polymorphv2.tpa~

BEGIN @12    // copy files
SUBCOMPONENT @10
INCLUDE ~%MOD_FOLDER%/install/polymorph.tpa~


//Patch for nymph.BCS (fixes crash with EEex)

BEGIN @21    // add NYMPH_CONFUSION (as dummy spell)
REQUIRE_PREDICATE GAME_IS ~iwdee~ @1
SUBCOMPONENT @20
LABEL ~D2-IWDEE-NYMPH~

FORBID_FILE ~override/d2cwb0.BCS~ @2    // check for Better IWD Pregen (patches will be skipped)
ACTION_IF (FILE_EXISTS ~override/nymph.BCS~) BEGIN WARN @3 END    // check for NYMPH.BCS in override

ADD_SPELL ~%MOD_FOLDER%/nymph/d2nymphc.SPL~ 3 7 NYMPH_CONFUSION
  SAY NAME1 ~Confusion~

COPY_EXISTING ~nymph.BCS~ ~override~
DECOMPILE_AND_PATCH BEGIN
  REPLACE_TEXTUALLY EXACT_MATCH ~3704~ ~NYMPH_CONFUSION~
END


BEGIN @22    // remove check for SPIN704
SUBCOMPONENT @20

ACTION_IF (FILE_EXISTS ~override/nymph.BCS~) BEGIN WARN @3 END   // check for NYMPH.BCS in override

COPY_EXISTING ~nymph.BCS~ ~override~
DECOMPILE_AND_PATCH BEGIN
  REPLACE_TEXTUALLY EXACT_MATCH ~Spell(NearestEnemyOf(Myself),3704)~ ~~
END


BEGIN @23    // cast CLERIC_CONFUSION
SUBCOMPONENT @20

ACTION_IF (FILE_EXISTS ~override/nymph.BCS~) BEGIN WARN @3 END    // check for NYMPH.BCS in override

COPY_EXISTING ~nymph.BCS~ ~override~
DECOMPILE_AND_PATCH BEGIN
  REPLACE_TEXTUALLY EXACT_MATCH ~3704~ ~CLERIC_CONFUSION~
END

