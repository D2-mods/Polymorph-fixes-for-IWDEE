/*
Polymorph fixes for IWD:EE
*/


//prep
ACTION_IF !(FILE_EXISTS_IN_GAME ~#EFF_M15.WAV~) BEGIN
  COPY ~%files%/#EFF_M15.WAV~  ~override~
END


//
//patches for Polymorph Self
COPY_EXISTING ~CDPOLYBB.ITM~  ~override~
              ~CDPOLYPB.ITM~  ~override~
              ~CDPOLYWW.ITM~  ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode=16 END    // remove permanent haste
BUT_ONLY


//
//patches for Shapechange

//water elemental damage (4d8)
COPY_EXISTING ~CDSHWELE.ITM~  ~override~
  LPF ALTER_ITEM_HEADER INT_VAR header_type=1 dicenumber=4 END
BUT_ONLY

//giant troll STR (18/00)
COPY_EXISTING ~SHTROLL.CRE~  ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
    WRITE_BYTE 0x238 18
    WRITE_BYTE 0x239 100
  END 
BUT_ONLY


//patch Psionic Blast
COPY_EXISTING ~SPIN974.SPL~  ~override~

  // prep
  LPF ALTER_SPELL_HEADER INT_VAR projectile=414 END    // default projectile for EE v2.6 (area cone)
  LPF DELETE_EFFECT INT_VAR match_opcode=324 END    // delete and re-add (in case of inconsistencies between game versions)
  LPF DELETE_EFFECT INT_VAR match_opcode=174 END
  LPF DELETE_EFFECT INT_VAR match_opcode=215 END
  LPF ALTER_EFFECT INT_VAR silent=1 match_duration=60 duration=30 END

  // racial protections
  LPF d2_protect_on_castequip INT_VAR opcode=324 cre_type=55 duration=100 STR_VAR insert=first END
  LPF CLONE_EFFECT INT_VAR match_opcode=324 parameter1=124 parameter2=104 STR_VAR insert=below END

  // visuals
  LPF ADD_SPELL_EFFECT INT_VAR opcode=174 target=2 timing=1 resist_dispel=2 savingthrow=1 STR_VAR resource=~#EFF_M15~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode=215 target=2 parameter2=0 timing=1 resist_dispel=2 savingthrow=1 STR_VAR resource=PARALH END
  LPF ADD_SPELL_EFFECT INT_VAR opcode=215 target=2 parameter2=1 timing=0 duration=2 resist_dispel=2 savingthrow=1 STR_VAR resource=SPMINDAT END

  // regenerate ability
  LPF DELETE_EFFECT INT_VAR match_opcode=171 STR_VAR match_resource=SPIN974 END
  LPF DELETE_EFFECT INT_VAR match_opcode=172 STR_VAR match_resource=SPIN974 END
  LPF ADD_SPELL_CFEFFECT INT_VAR opcode=171 target=1 timing=1 resist_dispel=2 STR_VAR resource=SPIN974 END
  LPF ADD_SPELL_CFEFFECT INT_VAR opcode=172 target=1 timing=1 resist_dispel=2 STR_VAR resource=SPIN974 END

BUT_ONLY


//text edits (polymorphs)
COPY_EXISTING ~SPIN198.SPL~  ~override~
  SAY NAME1 @100 SAY UNIDENTIFIED_DESC @200    // water elemental
BUT_ONLY
COPY_EXISTING ~SPIN157.SPL~  ~override~
  SAY NAME1 @101 SAY UNIDENTIFIED_DESC @201    // earth elemental
BUT_ONLY
COPY_EXISTING ~SPIN156.SPL~  ~override~
  SAY NAME1 @102 SAY UNIDENTIFIED_DESC @202    // fire elemental
BUT_ONLY
COPY_EXISTING ~SPIN154.SPL~  ~override~
  SAY NAME1 @103 SAY UNIDENTIFIED_DESC @203    // giant troll
BUT_ONLY
COPY_EXISTING ~SPIN153.SPL~  ~override~
  SAY NAME1 @104 SAY UNIDENTIFIED_DESC @204    // iron golem
BUT_ONLY
COPY_EXISTING ~SPIN152.SPL~  ~override~
  SAY NAME1 @105 SAY UNIDENTIFIED_DESC @205    // mind flayer
BUT_ONLY
COPY_EXISTING ~SPIN974.SPL~  ~override~
  SAY NAME1 @106 SAY UNIDENTIFIED_DESC @206    // psionic blast
BUT_ONLY
COPY_EXISTING ~SPIN155.SPL~  ~override~
  SAY NAME1 @107 SAY UNIDENTIFIED_DESC @207    // greater wolfwere
BUT_ONLY
COPY_EXISTING ~SPWI497.SPL~  ~override~
  SAY NAME1 @120 SAY UNIDENTIFIED_DESC @220    // brown bear
BUT_ONLY
COPY_EXISTING ~SPWI498.SPL~  ~override~
  SAY NAME1 @121 SAY UNIDENTIFIED_DESC @221    // black bear
BUT_ONLY


//
//